buildscript {
  repositories {
    mavenCentral()
  }
  dependencies {
    classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.1.2-4'
  }
}

apply plugin: "kotlin"
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
  mavenCentral()
}

dependencies {
  compile 'org.jetbrains.kotlin:kotlin-stdlib:1.1.2-4'
}

dependencies {
    compile 'io.reactivex.rxjava2:rxjavafx:2.1.0'
    testCompile 'junit:junit:4.12'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

group = "com.github.thomasnield"
archivesBaseName = "rxkotlinfx"
version = "2.1.0"

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'RxKotlinFX'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Interoperability library for RxJava, JavaFX, and Kotlin'
                url 'https://github.com/thomasnield/RxKotlinFX'

                scm {
                    connection 'scm:git:git://github.com/thomasnield/RxKotlinFX.git'
                    developerConnection 'scm:git:git@github.com:thomasnield/RxKotlinFX.git'
                    url 'https://github.com/thomasnield/RxKotlinFX.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'thomasnield'
                        name 'Thomas Nield'
                        email 'thomasnield@live.com'
                    }
                }
            }
        }
    }
}